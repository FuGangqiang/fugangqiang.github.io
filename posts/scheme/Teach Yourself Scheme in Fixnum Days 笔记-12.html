<!doctype html>
<html>
<head>
  <meta charset="utf-8">
  <meta name="generator" content="mdblog.rs">
  <meta name="viewport" content="width=device-width,initial-scale=1.0,maximum-scale=1.0,shrink-to-fit=no,user-scalable=0">
  <title>Teach Yourself Scheme in Fixnum Days 笔记-12</title>
<link rel="icon" href="https://fugangqiang.github.io/media/favicon.png">
  <link rel="stylesheet" href="https://fugangqiang.github.io/static/main.css"></head>
<body>
<header>
  <div class="container">
    <div id="site">
      <div id="site-name">
        <a href="https://fugangqiang.github.io/index.html" title="Fu">Fu</a>
      </div>
      <div id="site-motto">Simple is Beautiful!</div>
    </div>
    <nav id="header-nav">
      <a href="https://fugangqiang.github.io/index.html">博客</a>
      <a href="https://fugangqiang.github.io/tags.html">标签</a>
      <a href="https://fugangqiang.github.io/atom.xml">订阅</a>
    </nav>
    <svg id="menu" viewBox="0 0 1024 1024" version="1.1" xmlns="http://www.w3.org/2000/svg">
      <path d="M128 298.666667h768a42.666667 42.666667 0 0 0 0-85.333334H128a42.666667 42.666667 0 0 0 0 85.333334z m768 170.666666H128a42.666667 42.666667 0 0 0 0 85.333334h768a42.666667 42.666667 0 0 0 0-85.333334z m0 256H128a42.666667 42.666667 0 0 0 0 85.333334h768a42.666667 42.666667 0 0 0 0-85.333334z" fill="#fff"></path>
    </svg>
  </div>
</header>

<main class="container">
    <h1>Teach Yourself Scheme in Fixnum Days 笔记-12</h1>
    <article>
      <h1>第十二贴：对象和类</h1>
<p>类是用来描述拥有相似行为的一系列对象。由类描述的对象被称为该类的实例。类设定其实例各个槽<slot>的名字，而这些槽的值又因对象不同而不同。类也设定一系列以其实例为参数的方法<method>，槽的值可以为任何值，但是方法必须是一个过程。</p>
<p>类具有继承性，因此，一个类可以是另一个类&lt;被称为父类&gt;的子类，一个子类不仅有其自己的槽和方法，也继承了其父类的所有槽和方法。如果一个类和其父类拥有相同名字的槽或方法，难么该类会覆盖其父类的槽或方法。</p>
<h2>A simple object system</h2>
<p>我们实现了一个简单的 scheme 对象系统。这个系统只允许单继承&lt;一个类只能有一个父类&gt;，如果没有父类，用 <code>#t</code> 表示无父类，而 <code>#t</code> 的父类是其自身。</p>
<p>下面的 class 结构是用来定义类的，</p>
<pre><code>(defstruct class
   superclass slots method-names method-vector)
</code></pre>
<p>class 结构头两个域为 superclass 和 slots，分别指出父类和槽；而后两个域是 <code>method-names</code> 和 <code>method-vector</code>，用来表示类的方法。</p>
<p>这样我们就可以调用 <code>make-class</code> &lt;由 defstruct class 定义&gt; 来生成一个新的类&lt;无父类&gt;：</p>
<pre><code>(define trivial-bike-class
   (make-class
    #:superclass #t
    #:slots '(frmae parts size)
    #:method-names '()
    #:method-vector #()))
</code></pre>
<p><code>bike-class</code> 是一个非常简单的类，没有父类，没有方法，只有 <code>frame</code>、<code>parts</code>、<code>size</code> 三个槽。</p>
<p>复杂的类将会有父类和方法，这些就需要许多的初始化工作，为了简化初始化代码，我们可以写一个宏 <code>defclass</code>：</p>
<pre><code>(define-macro (defclass klass superclass slots . methods)
  `(define ,klass 
     (defclass-proc
       ,superclass
       (list ,@(map (lambda (slot) `',slot) slots))
       (list ,@(map (lambda (method) `',(car method)) methods))
       (vector ,@(map (lambda (method) `,(cadr method)) methods)))))
</code></pre>
<p>其中的 <code>defclass-proc</code> 过程见后文。
这样我们就可以用 <code>defclass</code> 宏来定义一个类：</p>
<pre><code>(defclass bike-class
  #t
  (frame size parts chain tires)
  (check-fit (lambda (self inseam)
               (let ((bike-size (slot-ref self 'size))
                     (ideal-size (* inseam 3/5)))
                 (let ((diff (- bike-size ideal-size)))
                   (cond ((&lt;= -1 diff 1) 'perfect-fit)
                         ((&lt;= -2 diff 2) 'fits-well)
                         ((&lt; diff -2) 'too-small)
                         ((&lt; diff 2) 'too-big)))))))
</code></pre>
<p>我们可以定义一个 <code>make-instance</code> 过程产生一个类的实例&lt;基于类定义的向量&gt;：</p>
<pre><code>(define (make-instance klass . slots-name-value)
  (let* ((slots (class.slots klass))
         (n (length slots))
         (instance (make-vector (+ n 1))))
    (vector-set! instance 0 klass)
    (let loop ((slots-name-value slots-name-value))
      (if (null? slots-name-value)
          instance
          (let ((i (list-position (keyword-&gt;symbol (car slots-name-value))
                                  slots)))
            (vector-set! instance (+ i 1) (cadr slots-name-value))
            (loop (cddr slots-name-value)))))))
</code></pre>
<p>其中的 list-position 过程见后文。</p>
<p>实例的格式特别简单：
实例向量的首个元素为其类型；
实例向量的其余元素为槽值<slot values>。
<code>make-instance</code> 的第一个参数为其类型，其后为一系列的序对&lt;对应槽名和槽值&gt;：</p>
<p>下面是用 <code>make-instance</code> 过程定义 <code>bike-class</code> 的一个实例：</p>
<pre><code>(define my-bike
  (make-instance bike-class
                 #:frame 'titanium
                 #:size 21
                 #:parts 'ultegra
                 #:chain 'sachs
                 #:tires 'continental))
</code></pre>
<p>我们也可以用 <code>defclass</code> 宏定义 <code>bike-class</code> 类的一个子类：</p>
<pre><code>(defclass mtn-bike-class
  bike-class
  (suspension)
  (check-fit (lambda (self inseam)
               (let ((bike-size (slot-ref self 'size))
                     (ideal-size (- (* inseam 3/5) 2)))
                 (let ((diff (- bike-size ideal-size)))
                   (cond ((&lt;= -2 diff 2) 'perfect-fit)
                         ((&lt;= -4 diff 4) 'fits-well)
                         ((&lt; diff -4) 'too-small)
                         ((&gt; diff 4) 'too-big)))))))
</code></pre>
<p>我们可以定义一个 <code>class-of</code> 过程来得到一个实例的类型：</p>
<pre><code>(define (class-of x)
  (if (vector? x)
      (let ((n (vector-length x)))
        (if (&gt;= n 1) 
            (let ((c (vector-ref x 0)))
              (if (class? c) c #t))
            #t))
      #t))
</code></pre>
<p>一个不是由 class 类产生的 scheme 对象的 <code>class-of</code> 被定义为 <code>#t</code> &lt;无类型&gt;。</p>
<p>过程 <code>slot-ref</code> 和 <code>slot-set!</code> 分别读取和改变类实例的槽值：</p>
<pre><code>(define (slot-ref instance slot)
  (let* ((klass (class-of instance))
         (slot-index (list-position slot (class.slots klass))))
    (vector-ref instance (+ slot-index 1))))
 
(define (slot-set! instance slot new-val)
  (let* ((klass (class-of instance))
         (slot-index (list-position slot (class.slots klass))))
    (vector-set! instance (+ slot-index 1) new-val)))
</code></pre>
<p>过程 <code>send</code> 是从实例的类中 <code>method-vector</code> 域寻找方法来对其实例进行操作：</p>
<pre><code>(define (send method instance . args)
  (let ((proc (let loop ((klass (class-of instance)))
                (if (eqv? klass #t)
                    (error 'send)
                    (let ((i (list-position method
                                            (class.method-names klass))))
                      (if i
                          (vector-ref (class.method-vector klass) i)
                          (loop (class.superclass klass))))))))
    (apply proc instance args)))
</code></pre>
<p>这样我们可以调用 <code>send</code> 来完成类方法的操作：</p>
<pre><code>(send 'check-fit my-bike 32)
</code></pre>
<p>以下是实现单继承类的代码：</p>
<pre><code>(define (list-position o l)
  (let loop ((i 0) (l l))
    (if (null? l)
        #f
        (if (eqv? (car l) o)
            i
            (loop (+ i 1) (cdr l))))))
 
(define (delete-duplicates s)
  (if (null? s)
      s
      (let ((a (car s))
            (d (cdr s)))
        (if (memv a d)
            (delete-duplicates d)
            (cons a (delete-duplicates d))))))
 
(define-macro (defstruct s . ff)
  (let ((s-s (symbol-&gt;string s))
        (n (length ff)))
    (let* ((n+1 (+ n 1))
           (vv (make-vector n+1)))
      (let loop ((i 1) (ff ff))
        (if (&lt;= i n)
            (let ((f (car ff)))
              (vector-set! vv i (if (pair? f)
                                    (cadr f)
                                    '*unspecified*))
              (loop (+ i 1) (cdr ff)))))
      (let ((ff (map (lambda (f) (if (pair? f)
                                     (car f)
                                     f))
                     ff)))
        `(begin
           (define ,(string-&gt;symbol
                     (string-append &quot;make-&quot; s-s))
             (lambda fvfv
               (let ((st (make-vector ,n+1))
                     (ff ',ff))
                 (vector-set! st 0 ',s)
                 ,@(let loop ((i 1) (r '()))
                     (if (&gt;= i n+1)
                         r
                         (loop (+ i 1)
                               (cons `(vector-set! st ,i ,(vector-ref vv i))
                                     r))))
                 (let loop ((fvfv fvfv))
                   (if (not (null? fvfv))
                       (begin (vector-set! st
                                           (+ (list-position (keyword-&gt;symbol (car fvfv))
                                                             ff)
                                              1)
                                           (cadr fvfv))
                              (loop (cddr fvfv)))))
                 st)))
           ,@(let loop ((i 1) (procs '()))
               (if (&gt;= i n+1)
                   procs
                   (loop (+ i 1)
                         (let ((f (symbol-&gt;string
                                   (list-ref ff (- i 1)))))
                           (cons
                            `(define ,(string-&gt;symbol
                                       (string-append s-s &quot;.&quot; f))
                               (lambda (x)
                                 (vector-ref x ,i)))
                            (cons
                             `(define ,(string-&gt;symbol
                                        (string-append &quot;set!&quot; s-s &quot;.&quot; f))
                                (lambda (x v)
                                  (vector-set! x ,i v)))
                             procs))))))
           (define ,(string-&gt;symbol (string-append s-s &quot;?&quot;))
             (lambda (x)
               (and (vector? x)
                    (eqv? (vector-ref x 0)
                          ',s)))))))))
 
(defstruct class
  superclass slots method-names method-vector)
 
(define-macro (defclass klass superclass slots . methods)
  `(define ,klass 
     (defclass-proc
       ,superclass
       (list ,@(map (lambda (slot) `',slot) slots))
       (list ,@(map (lambda (method) `',(car method)) methods))
       (vector ,@(map (lambda (method) `,(cadr method)) methods)))))
   
(define (defclass-proc superclass slots method-names method-vector)
  (make-class
   #:superclass superclass
   #:slots (let ((superclass-slots (if (eqv? superclass #t)
                                       '()
                                       (class.slots superclass))))
             (delete-duplicates (append slots superclass-slots)))
   #:method-names method-names
   #:method-vector method-vector))
 
(define (make-instance klass . slots-name-value)
  (let* ((slots (class.slots klass))
         (n (length slots))
         (instance (make-vector (+ n 1))))
    (vector-set! instance 0 klass)
    (let loop ((slots-name-value slots-name-value))
      (if (null? slots-name-value)
          instance
          (let ((i (list-position (keyword-&gt;symbol (car slots-name-value))
                                  slots)))
            (vector-set! instance (+ i 1) (cadr slots-name-value))
            (loop (cddr slots-name-value)))))))
 
(define (class-of x)
  (if (vector? x)
      (let ((n (vector-length x)))
        (if (&gt;= n 1) 
            (let ((c (vector-ref x 0)))
              (if (class? c) c #t))
            #t))
      #t))
 
(define (slot-ref instance slot)
  (let* ((klass (class-of instance))
         (slot-index (list-position slot (class.slots klass))))
    (vector-ref instance (+ slot-index 1))))
 
(define (slot-set! instance slot new-val)
  (let* ((klass (class-of instance))
         (slot-index (list-position slot (class.slots klass))))
    (vector-set! instance (+ slot-index 1) new-val)))
 
(define (send method instance . args)
  (let ((proc (let loop ((klass (class-of instance)))
                (if (eqv? klass #t)
                    (error 'send)
                    (let ((i (list-position method
                                            (class.method-names klass))))
                      (if i
                          (vector-ref (class.method-vector klass) i)
                          (loop (class.superclass klass))))))))
    (apply proc instance args)))
</code></pre>
<h2>Classes are instances too</h2>
<p>到此，我们已经介绍了一个简单的对象系统，但是聪明的读者也许会看出：其实类本身也是某类<metaclass>的实例。因为所有的类都具有相同的特性：都具有槽、父类、方法名字列表、方法向量。每个类看起来都有 <code>make-instance</code> 方法。这些这本身就符合“定义一个类来设定所有一些对象&lt;所有类&gt;共有的特性”。</p>
<p>下面我们将弃用上面定义的 class 结构，重新定义一个 class 向量<metaclass>来设定所有类的特性，同时 class 本身也是其自己的实例：</p>
<pre><code>(define class
  (vector 'value-of-class           ;类型&lt;随后设定&gt;
          #t                        ;父类
          (list 'superclass         ;槽
                'slots
                'method-names
                'method-vector)
          '(make-instance)          ;方法名     
          (vector make-instance)))  ;方法过程
(vector-set! class 0 class)         ;设定类型
</code></pre>
<p>这样我们就不能使用下面基于 class 结构的过程了：</p>
<pre><code>(class? x)(
(class.superclass c)
(class.slots c)
(class.method-names c)
(class.method-vector c)
(make-class ...)
</code></pre>
<p>需要用下面的方式来达到同样的目的：</p>
<pre><code>(and (vector? x)
     (eqv? (vector-ref x 0)
           class))
(vector-ref c 1)
(vector-ref c 2)
(vector-ref c 3)
(vector-ref c 4)
(send 'make-instance class ...)
</code></pre>
<h2>Multiple inheritance</h2>
<p>通过给 class 加一个 <code>precedence-list</code> 槽可以很容易实现多继承：</p>
<pre><code>(define class
  (vector 'value-of-class
          '()
          (list 'precedence-list
                'slots
                'method-names
                'method-vector)
          '(make-instance)
          (vector make-instance)))
(vector-set! class 0 class)
</code></pre>
<p>实现多继承的代码如下：</p>
<pre><code>(define (list-position o l)
  (let loop ((i 0) (l l))
    (if (null? l)
        #f
        (if (eqv? (car l) o)
            i
            (loop (+ i 1) (cdr l))))))
 
(define (delete-duplicates s)
  (if (null? s)
      s
      (let ((a (car s))
            (d (cdr s)))
        (if (memv a d)
            (delete-duplicates d)
            (cons a (delete-duplicates d))))))
 
(define (append-map f ls)
  (let loop ((ls ls))
    (if (null? ls)
        '()
        (append (f (car ls))
                (loop (cdr ls))))))
 
(define (class? x)
  (and (vector? x)
       (eqv? (vector-ref x 0)
             class)))
 
(define (class.precedence-list klass)
  (if (class? klass)
      (vector-ref klass 1)
      (error 'superclasses-of)))
 
(define (class.slots klass)
  (if (class? klass)
      (vector-ref klass 2)
      (error 'class-slots)))
 
(define (class.method-names klass)
  (if (class? klass)
      (vector-ref klass 3)
      (error 'class-method-names)))
 
(define (class.method-vector klass)
  (if (class? klass)
      (vector-ref klass 4)
      (error 'class-method-vector)))
 
(define (class-of instance)
  (if (vector? instance)
      (let ((n (vector-length instance)))
        (if (&gt;= n 1)
            (let ((klass (vector-ref instance 0)))
              (if (class? klass)
                  klass
                  #t))
            #t))
      #t))
 
(define (make-instance klass . slots-name-value)
  (let* ((slots (class.slots klass))
         (n (length slots))
         (instance (make-vector (+ n 1))))
    (vector-set! instance 0 klass)
    (let loop ((slots-name-value slots-name-value))
      (if (null? slots-name-value)
          instance
          (let ((i (list-position (keyword-&gt;symbol (car slots-name-value))
                                  slots)))
            (if i
                (vector-set! instance (+ i 1) (cadr slots-name-value))
                (error 'make-instance))
            (loop (cddr slots-name-value)))))))
 
(define class
  (vector 'value-of-class
          '()
          (list 'precedence-list
                'slots
                'method-names
                'method-vector)
          '(make-instance)
          (vector make-instance)))
(vector-set! class 0 class)
 
(define-macro (defclass klass direct-superclass-list slots . methods)
  `(define ,klass 
     (defclass-proc
       (list ,@(map (lambda (superclass) `,superclass) direct-superclass-list))
       (list ,@(map (lambda (slot) `',slot) slots))
       (list ,@(map (lambda (method) `',(car method)) methods))
       (vector ,@(map (lambda (method) `,(cadr method)) methods)))))
 
(define (defclass-proc direct-superclass-list slots method-names method-vector)
  (let ((precedence-list (delete-duplicates
                          (append direct-superclass-list
                                  (append-map (lambda (c) (class.precedence-list c))
                                              direct-superclass-list)))))
    (send 'make-instance
          class
          #:precedence-list precedence-list
          #:slots (delete-duplicates
                   (append slots (append-map
                                  (lambda (c) (class.slots c))
                                  precedence-list)))
          #:method-names method-names
          #:method-vector method-vector)))
 
(define (send method-name instance . args)
  (let ((proc (let ((klass (class-of instance)))
                (if (eqv? klass #t)
                    (error 'send)
                    (let loop ((klass klass)
                               (precedence-list (class.precedence-list klass)))
                      (let ((i (list-position method-name
                                              (class.method-names klass))))
                        (cond (i
                               (vector-ref (class.method-vector klass)
                                           i))
                              ((null? precedence-list)
                               (error 'send))
                              (else (loop (car precedence-list)
                                          (cdr precedence-list))))))))))
    (apply proc instance args)))
 
(define (slot-ref instance slot)
  (let* ((klass (class-of instance))
         (slot-index (list-position slot (class.slots klass))))
    (if slot-index
        (vector-ref instance (+ slot-index 1))
        (error 'slot-ref))))
 
(define (slot-set! instance slot new-val)
  (let* ((klass (class-of instance))
         (slot-index (list-position slot (class.slots klass))))
    (if slot-index
        (vector-set! instance (+ slot-index 1) new-val)
        (error 'slot-set!))))
 
(defclass bike-class
  ()
  (frame size parts chain tires)
  (check-fit (lambda (self inseam)
               (let ((bike-size (slot-ref self 'size))
                     (ideal-size (* inseam 3/5)))
                 (let ((diff (- bike-size ideal-size)))
                   (cond ((&lt;= -1 diff 1) 'perfect-fit)
                         ((&lt;= -2 diff 2) 'fits-well)
                         ((&lt; diff -2) 'too-small)
                         ((&lt; diff 2) 'too-big)))))))
 
(defclass mtn-bike-class
  (bike-class)
  (suspension)
  (check-fit (lambda (self inseam)
               (let ((bike-size (slot-ref self 'size))
                     (ideal-size (- (* inseam 3/5) 2)))
                 (let ((diff (- bike-size ideal-size)))
                   (cond ((&lt;= -2 diff 2) 'perfect-fit)
                         ((&lt;= -4 diff 4) 'fits-well)
                         ((&lt; diff -4) 'too-small)
                         ((&gt; diff 4) 'too-big)))))))
 
(define my-bike
  (send 'make-instance bike-class
        #:frame 'titanium
        #:size 21
        #:parts 'ultegra
        #:chain 'sachs
        #:tires 'continental))
</code></pre>

    </article>
    <div id="article-footer">
        <div>
          <svg class="icon" viewBox="0 0 1024 1024" version="1.1" xmlns="http://www.w3.org/2000/svg"><path d="M323.008 786.752c-52.928 0-96-43.072-96-96s43.072-96 96-96 96 43.072 96 96S375.936 786.752 323.008 786.752zM323.008 658.752c-17.632 0-32 14.336-32 32s14.368 32 32 32 32-14.336 32-32S340.64 658.752 323.008 658.752z" p-id="16156" fill="#bfbfbf"></path><path d="M416.096 927.072 284.224 927.072c-159.936 0-186.912-59.232-186.912-192l0-140.8c0-74.272 14.304-96.256 70.72-150.976l327.04-319.904c36.576-35.488 105.888-35.392 142.304-0.096l263.072 256.032c18.336 17.792 28.864 43.552 28.864 70.656 0 27.296-10.656 53.28-29.248 71.264l-290.016 294.592C544.544 880.416 497.216 927.072 416.096 927.072zM566.24 159.488c-10.496 0-20.16 3.52-26.528 9.696l-327.04 319.936c-49.952 48.48-51.36 54.528-51.36 105.152l0 140.8c0 110.272 8.352 128 122.912 128l131.872 0c52.672 0 83.744-28.48 148.992-92.8l26.656-26.144 263.232-268.256c6.784-6.592 10.336-15.808 10.336-25.888 0-9.888-3.424-18.88-9.472-24.736l-263.072-256.032C586.432 163.04 576.736 159.488 566.24 159.488z"></path></svg>
          <a href="https://fugangqiang.github.io/tags/scheme.html">scheme<sup>28</sup></a>
        </div>
      <div>
          <svg class="icon" viewBox="0 0 1024 1024" version="1.1" xmlns="http://www.w3.org/2000/svg"><path d="M512 192c179.2 0 320 140.8 320 320s-140.8 320-320 320-320-140.8-320-320S332.8 192 512 192M512 128C300.8 128 128 300.8 128 512s172.8 384 384 384 384-172.8 384-384S723.2 128 512 128L512 128z" p-id="1937" fill="#8a8a8a"></path><path d="M640 672c-6.4 0-19.2 0-25.6-6.4l-128-128C486.4 531.2 480 518.4 480 512L480 288C480 268.8 492.8 256 512 256s32 12.8 32 32l0 211.2 121.6 121.6c12.8 12.8 12.8 32 0 44.8C659.2 672 646.4 672 640 672z" p-id="1938"></path></svg>
          2011-01-03 10:45:00
        </div>
      </div>
</main>

<footer>
  <div class="container">博学之　审问之　慎思之　明辨之　笃行之</div>
</footer>

<script>
  function setMenu() {
    var menu = document.getElementById('menu');
    if (!menu) {
      return;
    }

    var headerNav = document.getElementById('header-nav');
    menu.addEventListener('click', function () {
      if (headerNav.style.display === 'flex') {
        headerNav.style.display = 'none';
      } else {
        headerNav.style.display = 'flex';
      }
    });

    document.addEventListener('click', function (evt) {
      if (!window.matchMedia('(max-width: 767px)').matches) {
        return;
      }
      if (headerNav.style.display !== 'flex') {
        return;
      }

      let targetElement = evt.target;
      do {
        if (targetElement == menu) {
          return;
        }
        targetElement = targetElement.parentNode;
      } while (targetElement)

      headerNav.style.display = 'none';
    })
  }

  window.addEventListener('load', setMenu);
</script>
<link rel="stylesheet" href="https://cdn.jsdelivr.net/npm/katex@0.13.20/dist/katex.min.css" integrity="sha384-cRxb1HsKSl8bTfU9fBcGsjktUfQa6w+fwvkYnU8XjFH4Qg8To1+/9OXv5iRzrKX4" crossorigin="anonymous">
<script defer src="https://cdn.jsdelivr.net/npm/katex@0.13.20/dist/katex.min.js" integrity="sha384-ov99pRO2tAc0JuxTVzf63RHHeQTJ0CIawbDZFiFTzB07aqFZwEu2pz4uzqL+5OPG" crossorigin="anonymous"></script>
<script>
  "use strict";
  document.addEventListener("DOMContentLoaded", function () {
    var maths = document.getElementsByClassName("language-math");
    for (var i = 0; i < maths.length; i++) {
      var el = maths[i];
      katex.render(el.innerText, el, { displayMode: true });
    }
    var codes = document.getElementsByTagName("code");
    for (i = 0; i < codes.length; i++) {
      el = codes[i];
      if (el.classList.contains("language-math")) continue;
      if (el.classList.contains("language-inline-math")) {
        katex.render(el.innerText, el);
        continue;
      }
      var n = el.nextSibling;
      var p = el.previousSibling;
      if (n && p && /^\$/.test(n.data) && /\$$/.test(p.data)) {
        katex.render(el.innerText, el);
        n.splitText(1);
        n.remove();
        p.splitText(p.data.length - 1).remove();
      }
    }
  });
</script>
</body>
</html>
